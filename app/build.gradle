
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'me.tatarka.retrolambda'


ext {
    kcProperties = new Properties()
    kcProperties.load(new FileInputStream(rootProject.file("keystore/kc-details.properties")));
}

def getKeyStoreFile = { ->
    file("../keystore/kc-release.jks")
}

def getKeyStorePassword = { ->
    kcProperties.keystorePassword
}

def getKeyAlias = { ->
    kcProperties.keyAlias
}

def getKeyPassword = { ->
    kcProperties.keyPassword
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.theappbusiness.kc"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "BACKEND_ENDPOINT", "\"${kc_backend_endpoint}\""
        manifestPlaceholders = [maps_key: "${kc_maps_key}"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("kc_debug.keystore")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
        release {
            storeFile getKeyStoreFile()
            storePassword getKeyStorePassword()
            keyAlias getKeyAlias()
            keyPassword getKeyPassword()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.2.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile "com.android.support:recyclerview-v7:23.1.0"
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.google.android.gms:play-services-maps:8.3.0'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.snappydb:snappydb-lib:0.5.2'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
    //enforcing 23.1.0 on tests
    androidTestCompile 'com.android.support:support-annotations:23.1.0'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.android.support:support-annotations:23.1.0'
    testCompile 'org.assertj:assertj-core:3.2.0'
    testCompile('org.powermock:powermock-api-mockito:1.6.3') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testCompile('org.powermock:powermock-module-junit4:1.6.3') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
}
